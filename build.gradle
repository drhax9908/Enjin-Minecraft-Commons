buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url 'https://plugins.gradle.org/m2/' }
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '1.2.4'
    id 'com.bmuschko.clover' version '2.2.0'
}

apply from: 'gradle/util.gradle'

subprojects {
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'com.bmuschko.clover'

    apply from: scriptFile('deps.gradle')

    group 'com.enjin.minecraft_commons'
    version '0.1.0-SNAPSHOT'

    idea {
        module {
            scopes.PROVIDED.plus += [configurations.shadow]
        }
    }

    configurations.getByName("clover").extendsFrom(configurations.shadow)

    tasks.build.dependsOn(shadowJar)
    artifacts.archives shadowJar

    tasks.withType(AbstractCompile) {
        classpath += configurations.shadow
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url 'https://oss.sonatype.org/content/groups/public/' }
    }

    dependencies {
        shadow deps.javaCommons

        testCompile deps.junit
        testCompile deps.mockito
        testCompile deps.powermockMockito
        testCompile deps.powermockJUnit4
        testCompile deps.assertj

        clover deps.clover
    }

    jar {
        archiveName = "${project.name}-slim.jar"
    }

    shadowJar {
        archiveName = "${project.name}.jar"
    }

    clover {
        testIncludes = ['**/*Test.java']

        report {
            html = true
            pdf = true
        }
    }
}
